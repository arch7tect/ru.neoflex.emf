package ru.neoflex.emf.bazi

import ru.neoflex.nfcore.bazi.natalChart.NatalChartFactory
import ru.neoflex.nfcore.bazi.natalChart.NatalChart
import ru.neoflex.nfcore.bazi.natalChart.Parameters
import ru.neoflex.nfcore.bazi.calendar.Calendar
import ru.neoflex.nfcore.bazi.calendar.CalendarFactory
import ru.neoflex.nfcore.bazi.calendar.CalendarPackage
import ru.neoflex.emf.restserver.DBServerSvc
import ru.neoflex.emf.base.HbTransaction
import java.util.stream.Collectors
import java.util.List
import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.resource.Resource
import ru.neoflex.nfcore.bazi.calendar.Year
import ru.neoflex.nfcore.bazi.natalChart.Pillar
import ru.neoflex.nfcore.bazi.natalChart.God
import ru.neoflex.nfcore.bazi.natalChart.Elements
import ru.neoflex.nfcore.bazi.calendar.impl.YearImpl

global DBServerSvc dbServerSvc;
global HbTransaction tx;

query "NatalCharts"
    $natalChart : NatalChart()
end

function Calendar createCalendar() {
    Calendar calendar = CalendarFactory.eINSTANCE.createCalendar();
    Year year = CalendarFactory.eINSTANCE.createYear();
    calendar.getYear().add(year);
    year.setName(2020);
    Pillar pillar = NatalChartFactory.eINSTANCE.createPillar();
    year.setYear(pillar);
    pillar.setGod(God.DIRECT_OFFICER);
    pillar.setSky(Elements.EARTH_YANG);
    return calendar;
}

rule "Create Calendar"
when
    not Calendar()
then
    Calendar calendar;
    List<Resource> calendars = tx.findByClass(CalendarPackage.eINSTANCE.getCalendar());
    if (calendars.size() > 0) {
        calendar = (Calendar) calendars.get(0).getContents().get(0);
    }
    else {
        calendar = createCalendar();
        Resource resource = tx.createResource();
        resource.getContents().add(calendar);
        resource.save(null);
    }
    insert(calendar);
end;

rule "Create NatalChart"
when
    $parameters: Parameters()
    not NatalChart(parameters == $parameters)
then
    NatalChart natalChart = NatalChartFactory.eINSTANCE.createNatalChart();
    natalChart.setParameters($parameters);
    insert(natalChart);
end;

rule "TEST Set Year Pillar"
when
    $natalChart: NatalChart($parameters: parameters, parameters != null, year == null)
    $calendar: Calendar($year: year)
    $currentYear: Year(name == $parameters.year) from $year
then
    $natalChart.setYear($currentYear.getYear());
    update($natalChart);
end;

